# arxiv:1605.03814 ATLAS JetMET

info analysis
# Details about experiment
  experiment ATLAS
  collider LHC
  id SUSY-2013-15
  inspireID 1458270
  title Search for squarks and gluinos in final states with jets and missing transverse momentum at $\sqrt{s} =13\,$TeV with the ATLAS detector
  publication Eur. Phys. J. C(2016) 76: 392
  sqrtS 13.0
  lumi 3.2
  arXiv 1605.03814
  hepdata http://hepdata.cedar.ac.uk/view/ins1304456
  doi 10.1140/epjc/s10052-016-4184-8

function calc_Meff
  arg jets
  arg MET
  code ATLASSUSY1605.03814_functions.h

function calc_dPhiMetJets
  arg jets
  arg njets
  arg MET
  code ATLASSUSY1605.03814_functions.h

function calc_METoverMeffNJ
  arg jets
  arg njets
  arg MET
  code ATLASSUSY1605.03814_functions.h

function calc_METoversqrtHT
  arg jets
  arg MET
  code ATLASSUSY1605.03814_functions.h

function calc_aplanarity
  arg cleanjets
  code ATLASSUSY1605.03814_functions.h

function unravelEl
  # Filter an electron collection by requiring a mininum of a 0.05 distance
  # in the \eta,\phi plane between the leptons. In case of multiple lepton
  # within this distance the first in the collection (pt ordered) 
  # is kept and others are dropped.
  arg electrons #collection to filter
  code ATLASSUSY1605.03814_functions.h

function dRPartVeto
  # Filter a particle collection by removing the element
  # closer in the \eta,\phi plane to any jet of a second
  # collection than a given deltaR minimum
  arg to_filter # collection to filter
  arg col2      # second collection
  code ATLASSUSY1605.03814_functions.h

function dRJetVeto
  # Filter a jet collection by removing the element
  # closer in the \eta,\phi plane to any particle of a second
  # collection than a given deltaR minimum
  arg to_filter # collection to filter
  arg col2      # second collection
  code ATLASSUSY1605.03814_functions.h


# OBJECT SELECTIONS
object jets 
  take external JetAk04-AtlasRun2-00
  select pt > 20
  select |eta| < 2.8

object cleanjets
  take jets
  apply dRJetVeto(col2 = electrons, minDeltaR = 0.2)

#object bjets
## b-tagging jets
#  take jets
#  select BTag = 1
#  select pt > 50
#  select |eta| < 2.5

object muons
# Muons
  take external Muon-AtlasRun2-00
  select pt > 10
  select |eta| < 2.7
  #select IsolationVarRhoCorr < 0.1
  #select isol(src=tracks, dR=0.4, reliso=true) < 0.1

object cleanmuons
  take muons
  apply dRPartVeto(col2 = cleanjets, minDeltaR = 0.4)
  

object electrons
# loose electrons
  take external Electron-AtlasRun2-00
  select pt > 10
  select |eta| < 2.47

object cleanelectrons
  take electrons
  apply dRPartVeto(col2 = cleanjets, minDeltaR = 0.4)

object verycleanelectrons
  take cleanelectrons
  apply unravelEl()

object MET
  take external Met-AtlasRun2-00

object jetsSR
  take cleanjets
  select pt > 50

# EVENT VARIABLES

variable Meff
  apply calc_Meff(jetsSR = jetsSR, MET = MET)

variable dPhiMet3j
  apply calc_dPhiMetJets(jetsSR = jetsSR, njets = 3, MET = MET)  

variable dPhiMetAllJets
  apply calc_dPhiMetJets(jetsSR = jetsSR, njets = -1, MET = MET)  

variable METoversqrtHT
  apply calc_METoversqrtHT(jets = cleanjets, MET = MET)

variable METoverMeff4j
  apply calc_METoverMeffNJ(jets = cleanjets, njets = 4, MET = MET)

variable METoverMeff5j
  apply calc_METoverMeffNJ(jets = cleanjets, njets = 5, MET = MET)

variable METoverMeff6j
  apply calc_METoverMeffNJ(jets = cleanjets, njets = 6, MET = MET)

variable aplanarity
  apply calc_aplanarity(jetsSR = jetsSR)


# EVENT SELECTION

cut preselection
# Pre-selection cuts
  select MET.pt > 200
  reject cleanmuons.size > 0
  reject verycleanelectrons.size > 0
  select jetsSR.size >= 2

cut 2jl
  select preselection
  select jetsSR[0].pt > 200
  select jetsSR.size >= 2
  select dPhiMet3j > 0.8
  select jetsSR[1].pt > 200
  select METoversqrtHT > 15
  select Meff > 1200

cut 2jm
  select preselection
  select jetsSR[0].pt > 300
  select jetsSR.size >= 2
  select dPhiMet3j > 0.4
  select jetsSR[1].pt > 50
  select METoversqrtHT > 15
  select Meff > 1600

cut 2jt

  select preselection
  select jetsSR[0].pt > 200
  select jetsSR.size >= 2
  select dPhiMet3j > 0.8
  select jetsSR[1].pt > 200
  select METoversqrtHT > 20
  select Meff > 2000

cut 4jt
  select preselection
  select jetsSR[0].pt > 200
  select jetsSR.size >= 4
  select dPhiMet3j > 0.4
  select dPhiMetAllJets > 0.2
  select jetsSR[1].pt > 100
  select jetsSR[2].pt > 100
  select jetsSR[3].pt > 100
  select aplanarity > 0.04
  select METoverMeff4j > 0.2
  select Meff > 2200

cut 5j
  select preselection
  select jetsSR[0].pt > 200
  select jetsSR.size >= 5
  select dPhiMet3j > 0.4
  select dPhiMetAllJets > 0.2
  select jetsSR[1].pt > 100
  select jetsSR[2].pt > 100
  select jetsSR[3].pt > 100
  select jetsSR[4].pt > 50
  select aplanarity > 0.04
  select METoverMeff5j > 0.25
  select Meff > 1600

cut 6jm
  select preselection
  select jetsSR[0].pt > 200
  select jetsSR.size >= 6
  select dPhiMet3j > 0.4
  select dPhiMetAllJets > 0.2
  select jetsSR[1].pt > 100
  select jetsSR[2].pt > 100
  select jetsSR[3].pt > 100
  select jetsSR[4].pt > 50
  select jetsSR[5].pt > 50
  select aplanarity > 0.04
  select METoverMeff6j > 0.25
  select Meff > 1600

cut 6jt
  select preselection
  select jetsSR[0].pt > 200
  select jetsSR.size >= 6
  select dPhiMet3j > 0.4
  select dPhiMetAllJets > 0.2
  select jetsSR[1].pt > 100
  select jetsSR[2].pt > 100
  select jetsSR[3].pt > 100
  select jetsSR[4].pt > 50
  select jetsSR[5].pt > 50
  select aplanarity > 0.04
  select METoverMeff6j > 0.2
  select Meff > 2000

# Results
table results_events
  type events
  columns name obs bkg   dbkg
  entry   2jl  263 283   24
  entry   2jm  191 191   21
  entry   2jt   26  23    4
  entry   4jt    7   4.6  1.1
  entry   5j     7  13.2  2.2
  entry   6jm    4   6.9  1.5
  entry   6jt    3   4.2  1.2
