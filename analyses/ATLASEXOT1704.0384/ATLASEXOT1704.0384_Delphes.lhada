# arxiv:1704.03848, ATLAS monophoton

info analysis
# Details about experiment
  experiment ATLAS
  id EXOT-2016-32
  publication Eur.Phys.J. C77 (2017) no.6, 393
  sqrtS 13.0
  lumi 36.1
  arXiv 1704.03848
  hepdata https://www.hepdata.net/record/ins1591328
  doi 10.1140/epjc/s10052-017-4965-8


# FUNCTIONS

function METoverSqrtSumET
  arg MET
  arg scalarHT
  code ATLASEXOT1704.0384_functions.h

# OBJECT SELECTIONS

object photons
# Delphes photons
  take Delphes_Photon
  select PT > 10
  select |Eta| < 2.37

object tightphotons
  take photons
  select |eta| < 1.37
  select |eta| > 1.52 and |eta| < 2.37  

object electrons
  take Delphes_Electron
  select PT > 7
  select |eta| < 2.7

object muons
  take Delphes_Muon
  select PT > 6
  select |eta| < 2.7

object jets 
# Delphes jets
  take Delphes_Jet
  select PT > 20

object cleanjets
  take jets
  apply dR(Eta, Phi, electrons.Eta, electrons.Phi) dRje
  reject dRje < 0.2

object cleanelectrons
  take electrons
  apply dR(Eta, Phi, cleanjets.Eta, cleanjets.Phi) dRlj
  reject dRlj < 0.4

object cleanmuons
  take muons
  apply dR(Eta, Phi, cleanjets.Eta, cleanjets.Phi) dRlj
  reject dRlj < 0.4

object verycleanjets
  take cleanjets
  apply dR(Eta, Phi, photons.Eta, photons.Phi) dRje
  reject dRje < 0.4

object jetsSR
  take verycleanjets
  select PT > 30
  select |Eta| < 4.5

object MET
  take Delphes_MissingET

object scalarHT
  # scalar sum of jets, electrons, photons, muons
  take Delphes_scalarHT


# VARIABLES

variable METoverSqrtSumET
  apply METoverSqrtSumET(MET, scalarHT)

# EVENT SELECTION

cut preselection
# Pre-selection cuts
  select photons[0].PT > 150
  select dphi(photon[0].Phi, MET.Phi) > 0.4
! select METoverSqrtSumET > 8.5
  reject jetsSR.size > 1
  reject dphi(jetsSR.Phi, MET.Phi) > 0.4
  select cleanmuons.size == 0
  select cleanelectrons.size == 0

cut SRI1
  select preselection
  select MET.PT > 150

cut SRI2
  select preselection
  select MET.PT > 225

cut SRI3
  select preselection
  select MET.PT > 300

cut SRE1
  select preselection
  select MET.PT > 150 and MET.PT < 225

cut SRE2
  select preselection
  select MET.PT > 225 and MET.PT < 300

